---
title: "TIL: Xá»­ lÃ½ máº£ng dynamic-ragged trong Excel"
description: "CÃ¡c máº£ng"
author:
  - name: "Tuan Le Khac"
    url: https://lktuan.github.io/
date: 06-06-2024
modified-date: 06-06-2024
categories: [excel, lambda, til] 
image: excel-lambda.jpg
draft: false
format: 
  html: 
    code-fold: false
    code-summary: "Show the code"
    code-line-numbers: true
css: html/styles.scss
fig-cap-location: bottom
editor: visual
---

# Problem statement

TIL: HÃ´m nay mÃ¬nh thá»­ giáº£i má»™t [challenge](https://www.linkedin.com/posts/owenhprice_data-analytics-sql-activity-7204129483144986624-R73I) cá»§a bÃ¡c [Owen Price](https://www.linkedin.com/in/owenhprice/). BÃ¡c lÃ  Microsoft MVP, master á»Ÿ khÃ­a cáº¡nh xá»­ lÃ­, lÃ m sáº¡ch, tháº­m chÃ­ lÃ  tá»‘i Æ°u viá»‡c xá»­ lÃ­ Ä‘Ã³ trong Excel, trang blog cá»§a bÃ¡c lÃ  [FLEX YOUR DATA](https://www.flexyourdata.com/), mÃ¬nh há»c Ä‘Æ°á»£c ráº¥t nhiá»u cÃ¡c sá»­ dá»¥ng formulas, Ä‘áº·c biá»‡t lÃ  cÃ¡c hÃ m má»›i tá»« `LAMBDA()`, `MAP()`, `REDUCE()` cho Ä‘áº¿n gáº§n Ä‘Ã¢y lÃ  **Python in Excel**, `GROUPBY()`, `PIVOTBY`, vÃ  cÃ¡c hÃ m `REGEX`.

Ná»™i dung challeng lÃ , vá»›i **má»™t pháº§n** (1000 records) cá»§a bá»™ dá»¯ liá»‡u [Citi Bike](https://citibikenyc.com/system-data), trong Ä‘Ã³ chá»©a hai cá»™t `start_station_name`, `end_station_name`, má»—i cá»™t láº¡i chá»©a thÃ´ng tin cÃ¡c "station" ~ tráº¡m, liá»‡t kÃª 5 **Ä‘á»‹a chá»‰** cÃ¹ng sá»‘ láº§n xuáº¥t hiá»‡n vá»›i sá»‘ lÆ°á»£ng lá»›n nháº¥t!. HÃ¬nh dÆ°á»›i lÃ  10 dÃ²ng Ä‘áº§u tiÃªn cá»§a dá»¯ liá»‡u.

::: {layout-ncol=1}
![10 dÃ²ng Ä‘áº§u tiÃªn cá»§a dá»¯ liá»‡u](data.png){width=100%}
:::

Well lÃºc Ä‘áº§u mÃ¬nh nghÄ© nÃ³ cÅ©ng Ä‘Æ¡n giáº£n ðŸ˜‚, tháº­m chÃ­ cÅ©ng khÃ´ng hiá»ƒu táº¡i sao má»—i start hoáº·c end station láº¡i cÃ³ *hai station*, cÃ¡ch nhau bá»Ÿi kÃ½ tá»± ` & ` (space before & after). MÃ¬nh láº­p tá»©c Ä‘Æ°a ra lá»i giáº£i nhÆ° sau:

``` txt
=LET(
    _d, " & ",
    _all_records, TOCOL(
        BYROW(
            BYROW(
                _tbl_CityBike[[start_station_name]:[end_station_name]],
                LAMBDA(x, TEXTJOIN(_d, TRUE, x))
            ),
        LAMBDA(x, TEXTSPLIT(x, _d))
        )
    ),
    _stations, UNIQUE(_all_records),
    _cnt, BYROW(
        --(_stations = TRANSPOSE(_all_records)), 
        // "--" equals to "+" will convert the boolean TRUE/FALSE to value 1/0
        LAMBDA(x, SUM(x))
    ),
    TAKE(SORTBY(HSTACK(_stations, _cnt), _cnt, -1), 5, )
)
```

Káº¿t quáº£:

::: {layout-ncol=1}
![Káº¿t quáº£ Ä‘áº§u tiÃªn cá»§a mÃ¬nh](my_initial_result.png){width=50%}
:::

Äá»‘i chiáº¿u vá»›i cÃ¡c káº¿t quáº£ cá»§a nhá»¯ng ngÆ°á»i tham gia khÃ¡ch thÃ¬ nÃ³ sai! Ã tÆ°á»Ÿng cá»§a mÃ¬nh lÃ  Ä‘áº§u tiÃªn vá»›i má»—i hÃ ng, join chÃºng láº¡i vá»›i ` & ` (`_d`), sau Ä‘Ã³ láº¡i split chÃºng ra vá»›i cÃ¹ng `_d` Ä‘Ã³, tá»« Ä‘Ã³ cÃ³ Ä‘Æ°á»£c danh sÃ¡ch occurence cá»§a táº¥t cáº£ cÃ¡c **Ä‘á»‹a chá»‰**. Tuy nhiÃªn mÃ¬nh nháº­n ra dá»¯ liá»‡u tráº£ vá» cho `_all_records` bá»‹ thiáº¿u sÃ³t. LÃ½ do lÃ  cÃ¡c tráº¡m (station) thÆ°á»ng lÃ  cÃ¡c giao lá»™, káº¿t há»£p tá»« hai Ä‘á»‹a chá»‰ vá»›i dáº¥u ` & `, tuy nhiÃªn cÃ³ má»™t sá»‘ tráº¡m láº¡i náº±m trÃªn má»™t con Ä‘Æ°á»ng. VÃ  vÃ¬ cháº¥t lÆ°á»£ng dá»¯ liá»‡u, cÅ©ng cÃ³ má»™t sá»‘ station lÃ  `null`. HÃ m `TOCOL()` lÃ  hÃ m máº£ng xá»­ lÃ­ cÃ¡ch máº£ng con nháº­n Ä‘Æ°á»£c tá»« `BYROW(...,LAMBDA(...,TEXTSPLIT()))`, cÃ¡c máº£ng con nÃ y cÃ³ Ä‘á»™ dÃ i **khÃ´ng Ä‘á»u**, dáº«n Ä‘áº¿n `TOCOL()` chá»‰ take record Ä‘áº§u tiÃªn cá»§a má»—i máº£ng con, tráº£ vá» `_all_records` chá»‰ gá»“m *1000 dÃ²ng dá»¯ liá»‡u*.

::: {layout-ncol=1}
![CÃ¡c Ä‘iá»ƒm dá»¯ liá»‡u Ä‘áº·c biá»‡t](special_data_points.png){width=100%}
:::

# Solution

CÃ¡c máº£ng lá»Ÿm chá»Ÿm hay ragged/jagged array lÃ  cÃ¡c máº£ng gá»“m cÃ¡c máº£ng con cÃ³ Ä‘á»™ dÃ i khÃ´ng Ä‘á»u nhau. [In computer science, a **jagged array**, also known as a **ragged array**, or **irregular array**, is an array of arrays of which the member arrays can be of different lengths, producing rows of jagged edges when visualized as output. In contrast, two-dimensional arrays are always rectangular, so jagged arrays should not be confused with multidimensional arrays, but the former is often used to emulate the latter. [Wiki](https://en.wikipedia.org/wiki/Jagged_array#:~:text=In%20computer%20science%2C%20a%20jagged,edges%20when%20visualized%20as%20output.)]{.aside}

Má»™t [comment](https://www.linkedin.com/feed/update/urn:li:activity:7204129483144986624?commentUrn=urn%3Ali%3Acomment%3A%28activity%3A7204129483144986624%2C7204309739827044353%29&dashCommentUrn=urn%3Ali%3Afsd_comment%3A%287204309739827044353%2Curn%3Ali%3Aactivity%3A7204129483144986624%29) Ä‘Æ°a mÃ¬nh tá»›i hÃ m [`BLAMBDAÎ»()`](https://gist.github.com/ncalm/0e72b08272ec14f411e8aaa763c0c0b5) cá»§a bÃ¡c MVP [Peter Bartholemew](https://www.linkedin.com/in/peterbartholomew/) vÃ  [bÃ i giáº£i thÃ­ch](https://www.linkedin.com/pulse/excel-lambda-spotlight-bisected-map-bmap%2525CE%2525BB-owen-price/?trackingId=8qiw9B83QGuOUZNX2I7OMg%3D%3D) cá»§a Owen Price.

::: {layout-ncol=1}
![Ragged or Jagged array, photo credit to [Geeks for Geeks](https://www.geeksforgeeks.org/jagged-array-in-java/)](g4g_ragged_array.png){width=80%}
::: 

VÃ  lá»i giáº£i Ä‘Ãºng, Ä‘Æ°á»£c dá»±a trÃªn Ã½ tÆ°á»Ÿng cá»§a `BLAMBDAÎ»()` lÃ :

``` txt
=LET(
    B, LAMBDA(B, v,
        LET(
            n, ROWS(v),
            IF(
                n > 1,
                VSTACK(B(B, TAKE(v, n / 2)), B(B, DROP(v, n / 2))),
                TEXTSPLIT(TEXTJOIN(" & ", , v), , " & ")
            )
        )
    ),
    str, B(B, CHOOSECOLS(_tbl_CityBike, 4, 5)),
    _u_str, UNIQUE(str),
    _cnt, BYROW(--(_u_str = TRANSPOSE(str)), LAMBDA(x, SUM(x))),
    TAKE(SORTBY(HSTACK(_u_str, _cnt), _cnt, -1), 5)
)

```

Náº¿u sá»­ dá»¥ng `GROUPBY()` thÃ¬ viá»‡c tÃ­nh toÃ¡n cuá»‘i cÃ¹ng sáº½ gá»n hÆ¡n, tuy nhiÃªn vÃ o thá»i Ä‘iá»ƒm viáº¿t bÃ i, hÃ m nÃ y váº«n chÆ°a Ä‘Æ°á»£c MS Ä‘Æ°a lÃªn 365 Production mÃ  chá»‰ á»Ÿ kÃªnh Insider.

Káº¿t quáº£ cuá»‘i cÃ¹ng:

::: {layout-ncol=1}
![Káº¿t quáº£ Ä‘Ãºng](final_result.png){width=50%}
:::

# `BLAMBDAÎ»()`

NguyÃªn vÄƒn má»¥c Ä‘Ã­ch cá»§a `BLAMBDAÎ»()` cá»§a Peter:

> Recursively bisects an array and applies a function to the leaf nodes. 
Useful for when the result of the FnÎ»(leaf) calls are not of equal length.
As such, BMAPÎ» can produce ragged arrays. 

Owen cÅ©ng nháº­n xÃ©t lÃ  hÃ m nÃ y "more computationally efficient" so vá»›i [`STACKER()`](https://gist.github.com/ncalm/ef7ed953571eec1475c291948aa2dbc3) cá»§a á»•ng. Chi tiáº¿t vá» Ã½ tÆ°á»Ÿng hÃ m gá»‘c cÃ³ thá»ƒ tham kháº£o á»Ÿ cÃ¡c link trÃªn, giá» mÃ¬nh sáº½ tÃ¬m hiá»ƒu `BLAMBDAÎ»()` Ä‘Æ°á»£c Ã¡p dá»¥ng cho use case nÃ y.

Äáº§u tiÃªn `B()` (stands for Bisected) lÃ  má»™t hÃ m recursive:

``` txt
B = LAMBDA(B, v,
        LET(
            n, ROWS(v),
            IF(
                n > 1,
                VSTACK(B(B, TAKE(v, n / 2)), B(B, DROP(v, n / 2))),
                TEXTSPLIT(TEXTJOIN(" & ", , v), , " & ")
            )
        )
    )
```

Trong Ä‘Ã³:

- `n` lÃ  sá»‘ dÃ²ng cá»§a `v` ~ máº£ng cáº§n tÃ­nh toÃ¡n
- náº¿u `n` > 1, tráº£ vá» káº¿t há»£p `VSTACK()` khi hÃ m `B()` Ä‘Æ°á»£c Ã¡p dá»¥ng cho hai ná»­a cá»§a máº£ng `v`, `TAKE()` sáº½ láº¥y "phÃ¢n ná»­a" Ä‘áº§u tiÃªn, `DROP()` sáº½ bá» Ä‘i phÃ¢n ná»­a Ä‘áº§u tiÃªn ~ nghÄ©a lÃ  láº¥y phÃ¢n ná»­a thá»© 2 (lÆ°u Ã½ ráº±ng hai hÃ m nÃ y Ä‘á»u láº¥y sá»‘ nguyÃªn nhá» hÆ¡n gáº§n nháº¥t cá»§a `n / 2`, do Ä‘Ã³ khÃ´ng cáº§n láº¥y pháº§n nguyÃªn báº±ng `QUOTIENT(n,2)` nhÆ° hÃ m gá»‘c cá»§a Peter).
- khi n = 1, nghÄ©a lÃ  vá»›i má»—i pháº§n tá»­ cá»§a máº£ng `v`, thá»±c hiá»‡n Ä‘á»™ng tÃ¡c `TEXTSPLIT(TEXTJOIN(...))` ~ trÆ°á»›c háº¿t join hai cá»™t rá»“i sau Ä‘Ã³ split.
- hÃ m nÃ y Ä‘á»‡ quy vÃ  sáº½ `VSTACK()` táº¥t cáº£ cÃ¡c máº£ng dá»c sinh tá»« viá»‡c `TEXTSPLIT()` káº¿t quáº£ cá»§a `TEXTJOIN()` hai cá»™t trÃªn má»—i dÃ²ng cá»§a máº£ng `v`.

MÃ¬nh cÅ©ng thá»­ viáº¿t láº¡i hÃ m `B()`, thay Ä‘á»•i má»™t chÃºt ~ thay vÃ¬ thá»±c hiá»‡n trÃªn 1 máº£ng 2 cá»™t mÃ¬nh cá»‘ gáº¯ng chuyá»ƒn máº£ng `v` thÃ nh má»™t cá»™t, tuy nhiÃªn chÆ°a thÃ nh cÃ´ng ðŸ˜ª **(chÆ°a hiá»ƒu táº¡i sao)**:

```txt
=LET(
    B, LAMBDA(B, v,
        LET(
            n, ROWS(v),
            IF(n > 1, VSTACK(B(B, TAKE(v, n / 2)), B(B, DROP(v, n / 2))), TEXTSPLIT(v, " & "))
        )
    ),
    _data, VSTACK(_tbl_CityBike[start_station_name], _tbl_CityBike[end_station_name]),
    str, B(B, FILTER(_data, LEN(_data) > 0)),
    _u_str, UNIQUE(str),
    _cnt, BYROW(--(_u_str = TRANSPOSE(str)), LAMBDA(x, SUM(x))),
    TAKE(SORTBY(HSTACK(_u_str, _cnt), _cnt, -1), 5)
)
```


Thanks Peter & Owen for this great use of recursion presented in such a concise manner. 


